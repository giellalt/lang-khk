Alphabet
! The alphabet should contain all symbols which are used in the grammar.
! Symbols consist of strings of utf-8 characters. Reserved words and white-space
! need to be quoted using %.
а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я 
!%{Б%} %{Ү%} %{Д%} %{А%} %{Э%} %{И%} %{Ө%} %{У%} %{Ч%} %{Х%} %{Й%} %{Ж%} %{Е%} %{Н%} %{Г%}  
%>:0 
%{Ы%}:0 %{Ы%}:и %{Ы%}:й %{Ы%}:ы %{а%}:а 
!%{Д%}:т 
!%{Б%}:б %{Б%}:в 
!%{Ү%}:а %{Ү%}:о %{Ү%}:э %{Ү%}:ө

;

Sets
C = б в г д ж з к л м н п р с т ф х ц ч ш щ е ё ю я %{Б%} %{Д%};
V = а и о ө у ү э ;
GG = э ү ө ; 
RR = а о у я ;
MN = м н г л б в р %{Б%};
ZZ = д ж з ц с т х ч ш ;
ZH = д ж з ц с т ч ш ;
ZS = д ж з ц т ч ш х ;
ZD = ж з ц т ч ш ;
ZT = д з ц с т х; 
TT = ч ш ж к ;
DP = г с р в ;
SS = ч ш ж г ;				
QL = б в д з л м н п р с т ф х ц е ё ю я ;
FK = ю я е ё ;
WW = м н л в ;
OO = а у я ;
FF = э ү и е ю ;
YY = а и о ө у ү э й ;
BB = э ү е ю ө ;
II = д ж з ц с т ч ш ;
JJ = г н ;
PP = с т м р ;
MM = м н г л б в ;
HH = д л ;
FI = э ү ;
OI = а и ;
BI = а и о ө у ү э ь ;
DS = д с ;


Rules 

"ЫЫ: ыЫ"
!улс %> %{Ы%} %{Ы%}   
!улс 0     0    ы    н  
%{Ы%}:ы <=> $[а|о|у] _ %{Ы%}:  ;

"ЫЫ: ы0"
%{Ы%}:0 <=> $[а|о|у] %{Ы%}:ы _  ;

!!€ улс>{Ы}{Ы}н
!!€ улс0ы0н

!!€ өрөө>{Ы}{Ы}н
!!€ өрөө0ы0н


"ЫЫ: иЫ"
!гэр %> %{Ы%} %{Ы%}   
!гэр 0    и     й    н  
%{Ы%}:и <=> $[э|ө|ү] _ %{Ы%}:й  ;

"ЫЫ: Ый"
%{Ы%}:й <=> $[э|ө|ү] %{Ы%}:и _  ;

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:о%{Ү%}" 
!бороо  %> г %{Ү%} %{Ү%}
!бороо  0  г о %{Ү%} 
%{Ү%}:о <=> $[о] (C:)* %> (:C)* _ %{Ү%}:  ;

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:%{Ү%}о" 
!бороо  0 г %{Ү%} о
%{Ү%}:о <=> $[о] (C:)* %> (:C)* %{Ү%}:о _ ; 

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:а%{Ү%}" 
%{Ү%}:а <=> $[а|у] (C:)* %> (:C)* _ %{Ү%}:  ;

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:%{Ү%}а" 
%{Ү%}:а <=> $[а|у] (C:)* %> (:C)* %{Ү%}:а _ ; 

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:э%{Ү%}" 
%{Ү%}:э <=> $[э|ү] (C:)* %> (:C)* _ %{Ү%}:  ;

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:%{Ү%}э" 
%{Ү%}:э <=> $[э|ү] (C:)* %> (:C)* %{Ү%}:э _ ; 

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:ө%{Ү%}" 
%{Ү%}:ө <=> $[ө] (C:)* %> (:C)* _ %{Ү%}:  ;

"Урт эгшиг зохицох: %{Ү%}%{Ү%}:%{Ү%}ө" 
%{Ү%}:ө <=> $[ө] (C:)* %> (:C)* %{Ү%}:ө _ ;

"Урт эгшиг зохицох: %{У%}%{У%}:у%{У%}" 
%{У%}:у <=> $[а|о|у] (C:)* %> (% ) (:C)* _ %{У%}:  ;

"Урт эгшиг зохицох: %{У%}%{У%}:%{У%}у" 
%{У%}:у <=> $[а|о|у] (C:)* %> (% ) (:C)* %{У%}:у _ ;

"Урт эгшиг зохицох: %{У%}%{У%}:ү%{У%}" 
%{У%}:ү <=> $[э|ө|ү|и] (C:)* %> (% ) (:C)* _ %{У%}:  ;

"Урт эгшиг зохицох: %{У%}%{У%}:%{У%}ү" 
%{У%}:ү <=> $[э|ө|ү|и] (C:)* %> (% ) (:C)* %{У%}:ү _ ;


"Богино эгшиг зохицох: %{Ү%}:а" 
%{Ү%}:а <=> $[а|у] (C:)* %> (:C)* _   ;

"Богино эгшиг зохицох: %{Ү%}:э" 
%{Ү%}:э <=> $[э|ү] (C:)* %> (:C)* _   ;

"Богино эгшиг зохицох: %{Ү%}:ө" 
%{Ү%}:ө <=> $[ө] (C:)* %> (:C)* _   ;

"Богино эгшиг зохицох: %{Ү%}:о" 
%{Ү%}:о <=> $[о] (C:)* %> (:C)* _   ;

"Богино эгшиг зохицох: %{Й%}:а" 
%{Й%}:а <=> $[а|у|я] (C:)* %> (:C)* _   ;

"Богино эгшиг зохицох: %{Й%}:э" 
%{Й%}:э <=> $[э|ү|ө|е] (C:)* %> (:C)* _   ;

"Богино эгшиг зохицох: %{Й%}:о" 
%{Й%}:о <=> $[о] (C:)* %> (:C)* _   ;
